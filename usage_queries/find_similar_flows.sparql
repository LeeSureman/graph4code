PREFIX graph4codeOntology: <http://purl.org/twc/graph4code/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graph4code: <http://purl.org/twc/graph4code/>
PREFIX sio: <http://semanticscience.org/resource/>

select (count(?g) as ?c) ?classifierName where {
    graph ?g {
      ?read rdfs:label "pandas.read_csv" .
	    ?read graph4code:flowsTo+ ?fit1 .

        ?classifier1 rdfs:label "sklearn.ensemble.RandomForestClassifier" .
        ?classifier1 graph4code:flowsTo/graph4code:flowsTo ?fit1 .
        ?classifier1 graph4code:flowsTo/sio:SIO_000230 ?anon .

        ?anon sio:SIO_000613  "0"^^<http://www.w3.org/2001/XMLSchema#int> .
        ?anon prov:isSpecializationOf ?fit1 .

        ?fit1 schema:about "fit" .

        ?read graph4code:flowsTo+ ?fit2 .

        ?classifier2 graph4code:flowsTo/graph4code:flowsTo ?fit2 .
        ?classifier2 graph4code:flowsTo/sio:SIO_000230 ?anon2 .

        ?anon2 sio:SIO_000613  "0"^^<http://www.w3.org/2001/XMLSchema#int> .
        ?anon2 prov:isSpecializationOf ?fit2 .

        ?fit2 schema:about "fit" .

        ?classifier2 rdfs:label ?classifierName.
        minus {
            ?classifier2 rdfs:label "sklearn.ensemble.RandomForestClassifier" .
        }
     }
} group by ?classifierName order by desc(?c)